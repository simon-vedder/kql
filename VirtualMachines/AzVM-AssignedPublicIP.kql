// =====================================================================
// .TITLE
//     AZVM-AssignedPublicIP
//
// .DESCRIPTION
//     Retrieves all virtual machines across all subscriptions that currently
//     have an assigned public IP address.
//     Joins VM resources with associated network interfaces and public IP resources.
//
// .REQUIRES
//
// .TAGS
//     Azure, ARG, Public IP, VM Inventory, Security, Networking
//
// .INPUT
//     Table: resources
//     Resource Types:
//       - microsoft.compute/virtualmachines
//       - microsoft.network/networkinterfaces
//       - microsoft.network/publicipaddresses
//
// .NOTES
//     - The query expands NICs and IP configurations for each VM
//     - Only VMs with directly associated public IPs are returned
//
// .AUTHOR
//     Simon Vedder (simonvedder.com)
//
// .VERSION
//     1.0
// =====================================================================

resources
| where type == "microsoft.compute/virtualmachines"
| extend nics = properties.networkProfile.networkInterfaces
| mv-expand nic = nics
| extend nicId = tostring(nic.id)
| join kind=leftouter (
    resources
    | where type == "microsoft.network/networkinterfaces"
    | project nicId = id, ipConfigs = properties.ipConfigurations
    | mv-expand ipConfig = ipConfigs
    | extend publicIpId = tostring(ipConfig.properties.publicIPAddress.id)
    | where isnotempty(publicIpId)
) on nicId
| join kind=leftouter (
    resources
    | where type == "microsoft.network/publicipaddresses"
    | project publicIpId = id, publicIpName = name, publicIpAddress = properties.ipAddress
) on publicIpId
| project 
    vmName = name,
    subscriptionId = tostring(split(id, '/')[2]),
    resourceGroup = tostring(split(id, '/')[4]),
    publicIpName,
    publicIpAddress
| where isnotempty(publicIpAddress)