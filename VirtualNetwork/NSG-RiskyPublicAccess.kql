// =====================================================================
// .TITLE
//     NSG-RiskyPublicAccess
//
// .DESCRIPTION
//     Identifies inbound NSG rules across all subscriptions that allow
//     potentially risky access from the public internet on commonly targeted ports.
//     Includes SSH (22), RDP (3389), and other sensitive ports (e.g. 1433, 3306, 1521).
//     Feel free to add additional ports!
//
// .REQUIRES
//
// .TAGS
//     Azure, ARG, NSG, Networking, Security, Public Access, Risk, Inventory
//
// .INPUT
//     Table: resources
//     Resource Type:
//       - microsoft.network/networksecuritygroups
//
// .NOTES
//     - The query expands all NSG security rules and filters for inbound rules
//       with "Allow" action and source set to "*" or public IP ranges.
//     - Only rules with destination ports commonly used in attacks are included.
//
// .AUTHOR
//     Simon Vedder (simonvedder.com)
//
// .VERSION
//     1.0 - 07.08.2025
// =====================================================================

resources
| where type == "microsoft.network/networksecuritygroups"
| extend nsgName = name, rules = properties.securityRules
| mv-expand rule = rules
| extend 
    ruleName = tostring(rule.name),
    access = tostring(rule.properties.access),
    direction = tostring(rule.properties.direction),
    priority = toint(rule.properties.priority),
    protocol = tostring(rule.properties.protocol),
    source = tostring(rule.properties.sourceAddressPrefix),
    port = tostring(rule.properties.destinationPortRange)
| where direction == "Inbound"
    and access == "Allow"
    and source in ("*", "0.0.0.0/0", "Internet")
    and port in ("22", "23", "80", "443", "1433", "3306", "3389", "1521", "5432", "27017", "5000")
| project nsgName, ruleName, protocol, port, source, access, direction, priority